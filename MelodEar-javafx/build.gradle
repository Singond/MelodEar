plugins {
	id "net.ltgt.apt" version "0.21"
	id "net.ltgt.apt-eclipse" version "0.21"
}
apply plugin: 'java'
apply plugin: 'eclipse'

dependencies {
	implementation rootProject
	implementation group: "com.github.singond", name: "music",
			version: "${versions.music}", changing: true
	implementation group: "com.github.singond", name: "music",
			classifier: "javadoc", version: "${versions.music}", changing: true

	// Logging
	implementation group: 'org.apache.logging.log4j',
			name: 'log4j-api', version: '2.1'
	implementation group: 'org.apache.logging.log4j',
			name: 'log4j-core', version: '2.1'

	// Dependency injection
	implementation group: 'javax.inject', name: 'javax.inject', version: '1'
	implementation 'com.google.dagger:dagger:2.23'
	annotationProcessor 'com.google.dagger:dagger-compiler:2.23'

	// Tests
	testImplementation group: 'junit', name: 'junit', version: '4.12'
}

sourceSets {
	root {
		resources {
			srcDirs = [rootProject.file("res")]
			include "img/icon-clef*"
		}
	}
	main.resources.source sourceSets.root.resources
	dev // For development
}

eclipse {
	project.name = 'MelodEar-javafx'
	classpath.downloadJavadoc = true
}

tasks.eclipse.dependsOn tasks.eclipseJdt
tasks.eclipseJdt.dependsOn tasks.eclipseFactorypath
tasks.eclipseFactorypath.dependsOn tasks.eclipseJdtApt

jar {
	destinationDir = file("${buildDir}/distributions")
	archiveBaseName = 'melodear'
	manifest {
		attributes 'Main-Class': 'com.github.singond.melodear.desktop.MelodEarApplication'
	}
	from {
		configurations.compile.collect {it.isDirectory() ? it : zipTree(it)}
	}
	rename 'icon-clef(.*)', 'icon$1'
}