plugins {
	id "net.ltgt.apt" version "0.21"
	id "net.ltgt.apt-eclipse" version "0.21"
	id 'org.openjfx.javafxplugin' version '0.0.9'
}
apply plugin: 'application'
apply plugin: 'eclipse'

dependencies {
	implementation rootProject
	implementation group: "com.github.singond", name: "music",
			version: "${versions.music}", changing: true
	implementation group: "com.github.singond", name: "music",
			classifier: "javadoc", version: "${versions.music}", changing: true
	implementation group: 'org.openjfx', name: 'javafx', version: '11.0.2',
			ext: 'pom'

	// Logging
	implementation group: 'org.apache.logging.log4j',
			name: 'log4j-api', version: '2.1'
	implementation group: 'org.apache.logging.log4j',
			name: 'log4j-core', version: '2.1'

	// Dependency injection
	implementation group: 'javax.inject', name: 'javax.inject', version: '1'
	implementation 'com.google.dagger:dagger:2.23'
	annotationProcessor 'com.google.dagger:dagger-compiler:2.23'

	// Tests
	testImplementation group: 'junit', name: 'junit', version: '4.12'
}

sourceSets {
	root {
		resources {
			srcDirs = [rootProject.file("res")]
			include "img/melodear-clefear*"
		}
	}
	main.resources.source sourceSets.root.resources
	dev // For development
}

javafx {
	version '11.0.2'
	modules = ['javafx.controls', 'javafx.fxml']
}

eclipse {
	project.name = 'MelodEar-javafx'
	classpath.downloadJavadoc = true
}

tasks.eclipse.dependsOn tasks.eclipseJdt
tasks.eclipseJdt.dependsOn tasks.eclipseFactorypath
tasks.eclipseFactorypath.dependsOn tasks.eclipseJdtApt

task fatJar(type: Jar) {
	destinationDirectory = file("${buildDir}/distributions")
	archiveBaseName = 'melodear'
	archiveAppendix = 'fat'
	manifest {
		attributes 'Main-Class': 'com.github.singond.melodear.desktop.MelodEarApplication'
	}
	from sourceSets.main.output
	from {
		configurations.compileClasspath.collect {
			if (it.exists()) {
				it.isDirectory() ? it : zipTree(it)
			} else {
				logger.error("Cannot find ${it}")
			}
		}
	}
}

application {
	mainClassName = 'com.github.singond.melodear.desktop.MelodEarApplication'
	applicationName = 'melodear'
}
