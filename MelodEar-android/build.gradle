apply plugin: 'com.android.application'
apply plugin: 'eclipse'

android {
	compileSdkVersion 25
	
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	defaultConfig {
		applicationId 'com.github.singond.melodear'
		minSdkVersion 21
		targetSdkVersion 25
		versionCode 1
		versionName '0.0.0'
	}
}

repositories {
	google()
	maven {url "https://jitpack.io"}
}

dependencies {
	implementation project(":")
	implementation 'com.github.billthefarmer:mididriver:v1.14'
	implementation group: "com.github.singond", name: "music",
			version: "${versions.music}", changing: true
	implementation group: "com.github.singond", name: "music",
			classifier: "javadoc", version: "${versions.music}", changing: true
}

eclipse {
	project.name = 'MelodEar-android'
}

// Export the dependencies to be used in IDE
task exportLib(type: Copy) {
	group "Development"
	description "Resolves the project's compile dependencies and exports them"
	from {
		configurations.debugCompileClasspath.collect {
			println "Exporting " + it
//			it.isDirectory() ? it : zipTree(it)
			if (it.getName().endsWith(".aar")) {
				return zipTree(it);
			} else {
				return it;
			}
		}
	}
	into "$buildDir/libs"
}

task cleanExportedLib(type: Delete) {
	group "Development"
	description "Removes the dependencies exported by 'exportLib'"
	delete "$buildDir/libs"
}
